#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПользовательИБ = ПользовательИБ();
	ОбновитьРоли(ПользовательИБ, Роли());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПользовательИБ()
	
	Если ЗначениеЗаполнено(УИ) Тогда
		Возврат ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(УИ);
	КонецЕсли;
	
	ПользовательИБ = ПользователиИнформационнойБазы.СоздатьПользователя();
	ПользовательИБ.Имя = Код;
	ПользовательИБ.ПолноеИмя = Наименование;
	ПользовательИБ.Записать();
	
	УИ = ПользовательИБ.УникальныйИдентификатор;
	
	Возврат ПользовательИБ;
	
КонецФункции

Функция Роли()
	
	Роли = Новый Массив;
	Роли.Добавить(Метаданные.Роли.ПолныеПрава);
	Возврат Роли;
	
КонецФункции

Процедура ОбновитьРоли(ПользовательИБ, Роли)
	
	ПользовательИБ.Роли.Очистить();
	Для Каждого Роль Из Роли Цикл
		ПользовательИБ.Роли.Добавить(Роль);
	КонецЦикла;
	
	ПользовательИБ.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
